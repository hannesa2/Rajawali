apply plugin: "org.jetbrains.dokka"
apply plugin: "digital.wup.android-maven-publish"
apply plugin: "signing"
apply plugin: "jacoco"

jacoco {
    toolVersion = "0.8.6"
    tasks.withType(Test).tap {
        configureEach {
            jacoco.includeNoLocationClasses = true
            jacoco.excludes = ["jdk.internal.*"]
        }
    }
}

tasks.withType(Test).configureEach {
    jacoco.includeNoLocationClasses = true
}

tasks.register("jacocoTestReport", JacocoReport) {
    group = "Reporting"
    description = "Combine code coverage to unified report."

    def ecSrc = fileTree(dir: "$project.buildDir", include: "**/*.ec")
    def execSrc = fileTree(dir: "$project.buildDir", include: "**/*.exec")

    doFirst {
        def files = files([ecSrc, execSrc]).files
        println "Creating Jacoco Report for ${files.size()} coverage files"
        files.each { file -> println file }
    }
}

publishing {
    repositories {
        def releaseUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
        def snapshotUrl = "https://oss.sonatype.org/content/repositories/snapshots"

        if (!version.contains("DIRTY")) {
            maven {
                url !version.contains("SNAPSHOT") ? releaseUrl : snapshotUrl
                credentials {
                    username System.getenv("SONATYPE_USERNAME") ?: ""
                    password System.getenv("SONATYPE_PASSWORD") ?: ""
                }
            }
        }
    }
}

signing {
    required false
    sign publishing.publications
}
