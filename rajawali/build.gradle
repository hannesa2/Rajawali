plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'kotlin-android'
    id "com.vanniktech.maven.publish" version "0.30.0"
}

android {
    namespace "org.rajawali3d"
    compileSdk rootProject.compileSdk

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion rootProject.targetSdk
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // VERSION_NAME no longer available as of 4.1
        // https://issuetracker.google.com/issues/158695880
        buildConfigField "String", "VERSION_NAME", "\"${getTag()}\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        buildConfig true
    }

    buildTypes {
        debug {
            testCoverageEnabled true
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    testOptions {
        animationsDisabled true
        unitTests {
            returnDefaultValues = true
            includeAndroidResources = true
        }
    }
    lint {
        abortOnError false
    }
}

dependencies {
    implementation "androidx.annotation:annotation:1.9.1"
    api "com.github.AppDevNext.Logcat:LogcatCoreLib:3.3.1"
    testImplementation "junit:junit:4.13.2"

    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"
    androidTestImplementation "androidx.test:rules:1.5.0"
    androidTestImplementation "androidx.test:runner:1.5.2"
    androidTestImplementation "androidx.test:core:1.5.0"

    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test.ext:truth:1.5.0"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

mavenPublishing {
    pom {
        name = "Rajawali"
        description = "Android OpenGL ES 2.0/3.0 Engine"
        inceptionYear = "2025"
        url = "https://github.com/hannesa2/Rajawali"
        licenses {
            license {
                name = "The Apache License, Version 2.0"
                url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }
        developers {
            developer {
                id = "hannesa2"
                name = "hannesa2"
                url = "https://github.com/hannesa2/"
            }
        }
        scm {
            url = "https://github.com/hannesa2/Rajawali/"
            connection = "scm:git:git://github.com/hannesa2/Rajawali.git"
            developerConnection = "scm:git:git://github.com/hannesa2/Rajawali.git"
        }
    }

    // Github packages
    repositories {
        maven {
            version = version+"-SNAPSHOT"
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/hannesa2/Rajawali"
            credentials {
                username = System.getenv("GITHUBACTOR")
                password = System.getenv("GITHUBTOKEN")
            }
        }
    }
}
